# Employment of single-edged but two-handed swords shows a Japanese influence.

PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX cucoo: <http://www.semanticweb.org/paulagranadosgarcia/CuCoO/>
PREFIX ma: <http://crossings.github.io/term/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
  # Feature-based association
  ?style cucoo:hasCulturalAssociation ?assoc1 , ?assoc2 .
  ?assoc1 a cucoo:CulturalAssociation
    ; cucoo:isAssociatedWith ?uident
    ; cucoo:hasCulturalContactTrait ?feature
    ; cucoo:value ?featureValue
  .
  # Weapon-based association
  ?assoc2 a cucoo:CulturalAssociation
    ; cucoo:isAssociatedWith ?uident
    ; cucoo:hasCulturalContactTrait ma:WeaponType
    ; cucoo:value ?weapon
  .
}
WHERE {
  ?style a ma:StylingSystem
    # uses weapon either directly or through a technique
    ; ^ma:belongsInSystem?/ma:employsArmament ?weapon
  .
  ?weapon skos:broader+ ?weaponType
    ; ^ma:handles [ ?feature ?featureValue ]
  .
  VALUES (?weaponType ?featureValue ?feature ?ident) {
	( <http://data.unil.ch/weapon/crossings/singleedgedsword> ma:TwoHandedGrip ma:withGrip "Japanese")
  }
  ?featureValue a ?featureType .
  
  # Functions to build new entities
  BIND (URI(CONCAT(STR(?style),"/assoc/", LCASE(?ident) ,"/", REPLACE( LCASE(STR(?feature)), '^.+/', '') )) AS ?assoc1)
  BIND (URI(CONCAT(STR(?style),"/assoc/", LCASE(?ident) ,"/weapon")) AS ?assoc2)
  BIND (URI(CONCAT("http://data.unil.ch/cultural_identity/crossings/", LCASE(?ident) )) AS ?uident)

}
