# definition of prefixes
PREFIX cucoo:
<http://www.semanticweb.org/paulagranadosgarcia/CuCoO/>
PREFIX epi: <http://edh-www.adw.uni-heidelberg.de/edh/ontology#>
# construct clause with the triples to generate
CONSTRUCT {
?x cucoo:hasCulturalAssociation ?assoc .
?assoc a cucoo:CulturalAssociation
; cucoo:isAssociatedWith ?uident
; cucoo:hasCulturalContactTrait
<http://data.open.ac.uk/erub/culturalTrait/clothing>
; cucoo:value ?clothes
}
WHERE {
?x epi:hasPerson ?person .
?person cucoo:wears ?clothes .
# A table that associates cultural identity labels to clothing
VALUES (?clothes ?ident) {
( "chiton"@en "Iberian")
( "cross_strips"@en "Iberian")
( "Tunic"@en "Roman" )
( "Toga"@en "Roman" )
( "Tunic_and_weapons"@en "Roman" )
( "Tunic_palla"@en "Roman" )
( "bulla_and_Toga_praetexta"@en "Roman" )
}
# functions to automatically construct a new URI for the
# association based on the URI of the object
BIND (URI(CONCAT(STR(?x),"/assoc/", LCASE(?ident) ,"/clothing"))
AS ?assoc)
BIND (URI(CONCAT("http://data.open.ac.uk/erub/cultural_identity/",
LCASE(?ident) )) AS ?uident)
}